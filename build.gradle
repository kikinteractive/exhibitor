ext.githubProjectName = rootProject.name // Change if github project name is not the same as the root project's name

buildscript {
    repositories { mavenCentral() }
    apply from: file('gradle/buildscript.gradle'), to: buildscript 
    dependencies { classpath 'com.trigonic:gradle-rpm-plugin:1.1' }
}

allprojects {
    repositories { mavenCentral() }
}

apply from: file('gradle/convention.gradle')
apply from: file('gradle/maven.gradle')
//apply from: file('gradle/check.gradle')
//apply from: file('gradle/license.gradle') // Waiting for re-release
apply from: file('gradle/release.gradle')

apply plugin: 'idea'
apply plugin: 'eclipse'

subprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'java'

    group = "com.netflix.${githubProjectName}" // TEMPLATE: Set to organization of project

    dependencies {
        testCompile 'org.testng:testng:6.1.1'
    }
}

project(':exhibitor-core') {
    dependencies {
        compile('org.apache.zookeeper:zookeeper:3.4.4') {
            exclude group: 'com.sun.jmx', module: 'jmxri'
            exclude group: 'com.sun.jdmk', module: 'jmxtools'
            exclude group: 'javax.jms', module: 'jms'

            // JLine pulls this in as a compile dependency, they have fixed it in future versions
            exclude group: 'junit', module: 'junit'
        }
        compile 'com.netflix.curator:curator-framework:1.2.3'
        compile 'com.netflix.curator:curator-recipes:1.2.3'
        compile 'com.netflix.servo:servo-core:0.4.20'
        compile 'com.google.guava:guava:11.0.1'
        compile 'javax.ws.rs:jsr311-api:1.1.1'
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.8.3'
        compile 'org.apache.lucene:lucene-core:3.6.0'
        compile 'com.sun.jersey:jersey-client:1.11'

        // if you are using Java 7 you can remove this and switch to the JDK version
        compile 'org.codehaus.jsr166-mirror:jsr166y:1.7.0'

        compile 'com.amazonaws:aws-java-sdk:1.3.11'   // should be provided - gradle doesn't support
        compile 'com.sun.jersey:jersey-bundle:1.9.1'  // should be provided - gradle doesn't support
        compile 'com.sun.xml.bind:jaxb-impl:2.2.4'   // should be provided - gradle doesn't support

        testCompile 'com.netflix.curator:curator-test:1.2.3'
        testCompile 'org.mortbay.jetty:jetty:6.1.22'
        testCompile 'org.mockito:mockito-core:1.8.5'
    }

    jar {
        manifest {
            attributes (
                'Implementation-Title': 'exhibitor-core',
                'Implementation-Version': project.version
            )
        }
    }
}

project(':exhibitor-standalone') {
    apply plugin: 'rpm'
    dependencies {
        compile (project(":exhibitor-core")) {
            exclude group: 'com.sun.jersey', module: 'jersey-client'
        }
        compile 'com.sun.jersey:jersey-bundle:1.9.1'
        compile 'com.sun.xml.bind:jaxb-impl:2.2.4'
        compile 'javax.servlet:servlet-api:2.5'    // should be provided - gradle doesn't support
        compile 'org.mortbay.jetty:jetty:6.1.22'
        compile 'com.google.guava:guava:11.0.1'
        compile 'commons-cli:commons-cli:1.2'
        compile 'com.amazonaws:aws-java-sdk:1.3.11'
        compile 'org.slf4j:slf4j-log4j12:1.7.0'
    }

    jar {
        manifest {
            attributes (
                    'Main-Class': 'com.netflix.exhibitor.application.ExhibitorMain',
                    'Implementation-Version': project.version
                    )
        }
    }

    task rpm(type:Rpm) {
        packageName = 'exhibitor'
        release = 1
        version = project.version.replaceAll('SNAPSHOT', new Date().format("yyyyMMddhhmmss"))
        arch = NOARCH
        os = LINUX

        into '/opt/exhibitor/'
        from (jar.outputs.files) { 
            into 'lib' 
        }
        from (configurations.compile) {
            into 'lib'
        }
        
        from ('src/main/script') {
            into 'bin'
            fileMode = 0755
        }
        
        from ('src/main/config') {
            into '/etc/exhibitor/'
        }
        
        link('/etc/init.d/exhibitor', '/opt/exhibitor/bin/exhibitor.sh')
    }
}

